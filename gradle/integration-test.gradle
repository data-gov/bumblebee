sourceSets {
  integrationTest {
    kotlin.srcDir file('src/test/integration/kotlin')
    resources.srcDir file('src/test/integration/resources')
    compileClasspath += sourceSets.main.output + configurations.testRuntime
    runtimeClasspath += output + compileClasspath
  }
}

configurations {
  integrationTestImplementation.extendsFrom testImplementation {
    exclude group: "ch.qos.logback", module: "logback-classic"
  }
  integrationTestRuntime.extendsFrom testRuntimeOnly
}

task integrationTest(type: Test) {
  useJUnitPlatform()
  failFast = true

  description = 'Runs integration tests.'
  group = 'verification'

  testClassesDirs = sourceSets.integrationTest.output.classesDirs
  classpath = sourceSets.integrationTest.runtimeClasspath
  mustRunAfter test
}

dependencies {
  integrationTestRuntime 'org.junit.jupiter:junit-jupiter-engine:5.1.0'
}

check.dependsOn integrationTest
